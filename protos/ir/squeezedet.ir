#define DTYPE TL_FLOAT

#define conv(in_name, out_name, in_c, out_c,                            \
             _group, _size, _stride, _padding, _dilation)               \
    create(| dst=out_name##_wts |                                       \
           dtype=DTYPE, dims=[_group, out_c, in_c, _size, _size], data=[0]); \
    create(| dst=out_name##_bias | dtype=DTYPE, dims=[out_c], data=[0]); \
    conv2d(src=in_name, weight=out_name##_wts, bias=out_name##_bias |   \
           dst=out_name | group=_group, size=[_size, _size],            \
           stride=[_stride, _stride],                                   \
           padding=[_padding, _padding, _padding, _padding],            \
           dilation=[_dilation, _dilation])

#define conv_relu(in_name, out_name, in_c, out_c,               \
                  _group, _size, _stride, _padding, _dilation)  \
    conv(in_name, out_name##_conv, in_c, out_c,                 \
         _group, _size, _stride, _padding, _dilation);          \
    relu(src=out_name##_conv | dst=out_name |)

#define fire(in_name, out_name, in_c, ns1x1, ne1x1, ne3x3)              \
    conv_relu(in_name, out_name##_sq1x1, in_c, ns1x1, 1, 1, 1, 0, 1);   \
    conv_relu(out_name##_sq1x1, out_name##_ex1x1, ns1x1, ne1x1,         \
              1, 1, 1, 0, 1);                                           \
    conv_relu(out_name##_ex1x1, out_name##_ex3x3, ne1x1, ne3x3,         \
              1, 3, 1, 1, 1);                                           \
    concat(src1=out_name##_ex1x1, src2=out_name##_ex3x3 | dst=out_name | \
           axis=1)

#define CONVOUT_C 108
#define CLASS_SLICE_C 63
#define CONF_SLICE_C 9
#define CLASS_PLUS_CONF_SLICE_C 71
#define BBOX_SLICE_C 36
#define ANCHORS_PER_GRID 9
#define OUTPUT_CLS_SIZE 7
#define OUTPUT_BBOX_SIZE 4
#define CONVOUT_H 24
#define CONVOUT_W 78

create(| dst=img | dtype=DTYPE, dims=[1, 3, 384, 1248], data=[0]);
conv_relu(img, conv0, 3, 64, 1, 3, 2, 1, 1);
maxpool2d(src=conv0 | dst=pool0 | size=[3, 3], stride=[2, 2],
          padding=[1, 1, 1, 1]);
fire(pool0, fire0, 64, 16, 64, 64);
fire(fire0, fire1, 128, 16, 64, 64);
maxpool2d(src=fire1 | dst=pool1 | size=[3, 3], stride=[2, 2],
          padding=[1, 1, 1, 1]);
fire(pool1, fire2, 128, 32, 128, 128);
fire(fire2, fire3, 256, 32, 128, 128);
maxpool2d(src=fire3 | dst=pool2 | size=[3, 3], stride=[2, 2],
          padding=[1, 1, 1, 1]);
fire(pool2, fire4, 256, 48, 192, 192);
fire(fire4, fire5, 384, 48, 192, 192);
fire(fire5, fire6, 384, 64, 256, 256);
fire(fire6, fire7, 512, 64, 256, 256);
fire(fire7, fire8, 512, 96, 384, 384);
fire(fire8, fire9, 768, 96, 384, 384);
conv(fire9, conv1, 768, CONVOUT_C, 1, 3, 1, 1, 1);
slice(src=conv1 | dst=slice0 | axis=1, start=0, len=CLASS_SLICE_C);
slice(src=conv1 | dst=slice1 | axis=1, start=CLASS_SLICE_C, len=CONF_SLICE_C);
slice(src=conv1 | dst=slice2 | axis=1, start=CLASS_PLUS_CONF_SLICE_C,
      len=BBOX_SLICE_C);
softmax(src=slice0 | dst=softmax0 | axis=1);
sigmoid(src=slice1 | dst=sigmoid0 |);
reshape(src=softmax0 | dst=reshape0 |
        dims=[1, ANCHORS_PER_GRID, OUTPUT_CLS_SIZE, CONVOUT_H, CONVOUT_W]);
reshape(src=sigmoid0 | dst=reshape1 |
        dims=[1, ANCHORS_PER_GRID, 1, CONVOUT_H, CONVOUT_W]);
reshape(src=slice2 | dst=reshape2 |
        dims=[1, ANCHORS_PER_GRID, OUTPUT_BBOX_SIZE, CONVOUT_H, CONVOUT_W]);
transpose(src=reshape0 | dst=transpose0 | axes=[0, 3, 4, 1, 2]);
transpose(src=reshape1 | dst=transpose1 | axes=[0, 3, 4, 1, 2]);
transpose(src=reshape2 | dst=transpose2 | axes=[0, 3, 4, 1, 2]);
